extend class UaS_Scarcity_Handler {
	bool DoMedical(Actor mo) {
		// false = "I'm not handling this"
		if (!mo) { return false; }
		bool ValidClass = (
			mo is "PortableStimpack"
			|| mo is "PortableMedikit"
			|| mo is "PortableBerserkPack"
			|| mo is "SecondBlood"
		);
		if (!ValidClass) { return false; }

		bool dbgOverspawned = false;

		// true = "I handled this"
		switch(mo.GetClassName()) {
			case 'PortableStimpack':
				if(MedSpawnCheck()) { dbgOverspawned=true; }
				else {
					HDCore.SpawnStuff('SpentStim', mo.pos, random(0, 1));
					HDCore.SpawnStuff('UaS_EmptyStimpack', mo.pos);
					mo.destroy();
				}
				break;
			case 'PortableMedikit': 
				// break open bonus contents
				if(UaS.RandomChance(UaS_ScarcityOverspawn)) { HDCore.SpawnStuff('PortableStimpack', mo.pos); }
				if(UaS.RandomChance(UaS_ScarcityOverspawn)) { HDCore.SpawnStuff('SecondBlood', mo.pos); }
				// handle autoinjector
				if(MedSpawnCheck()) {
					dbgOverspawned=true;
					let spawnedMedikit = HDMedikitter(actor.Spawn('HDMedikitter', mo.pos));
					if(!spawnedMedikit) { break; }
					//assume 7 auto-sutures per module, default 42 sutures = 6 modules
					int maxmodules = int(max(1, (MEDIKIT_MAXFLESH * UaS_ScarcityOverspawn) / 6));
					int kitmodules = max(1, random(1, maxmodules));
					spawnedMedikit.weaponstatus[MEDS_SECONDFLESH] = 7 * kitmodules;
					if(spawnedMedikit.weaponstatus[MEDS_SECONDFLESH] < MEDIKIT_MAXFLESH) { spawnedMedikit.frame = 2; }
					if(UaS.RandomChance(UaS_ScarcityOverspawn)) { HDCore.SpawnStuff('UaS_2FCartridge', mo.pos); }
				}
				HDCore.SpawnStuff('UaS_EmptyMedikit', mo.pos);
				mo.destroy();
				break;
			case 'PortableBerserkPack':
				if(MedSpawnCheck()) { dbgOverspawned=true; }
				else {
					HDCore.SpawnStuff('SpentZerk', mo.pos, random(0, 1));
					HDCore.SpawnStuff('UaS_EmptyZerkpack', mo.pos);
					mo.destroy();
				}
				break;
		}

		if (dbgOverspawned) { dbgMsg = dbgMsg.."\n\t• overspawned or retained medical item "..mo.getclassname(); }
		else { dbgMsg = dbgMsg.."\n\t• removed medical item "..mo.getclassname(); }
		return true;
	}

	bool MedSpawnCheck() {
		return (
			UaS.RandomChance(UaS_ScarcityOverspawn)
			|| UaS_ScarcityKeepMedical
		);
	}
}


//Replacer routines for medical items
